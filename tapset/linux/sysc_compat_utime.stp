# long compat_sys_utime(char __user *filename, struct compat_utimbuf __user *t)

@define _SYSCALL_COMPAT_UTIME_NAME
%(
	name = "utime"
%)

@define _SYSCALL_COMPAT_UTIME_ARGSTR
%(
	argstr = sprintf("%s, [%s, %s]", filename,
	                 ctime(actime), ctime(modtime))
%)

probe syscall.compat_utime = dw_syscall.compat_utime !,
                             nd_syscall.compat_utime ? {}
probe syscall.compat_utime.return = dw_syscall.compat_utime.return !,
                                    nd_syscall.compat_utime.return ? {}

# dw_compat_utime _____________________________________________________

probe dw_syscall.compat_utime = kernel.function("compat_sys_utime").call ?
{
	@_SYSCALL_COMPAT_UTIME_NAME
	filename_uaddr = $filename
	filename = user_string_quoted($filename)
	buf_uaddr = $t
	actime = _struct_compat_utimbuf_actime(buf_uaddr)
	modtime = _struct_compat_utimbuf_modtime(buf_uaddr)
	@_SYSCALL_COMPAT_UTIME_ARGSTR
}
probe dw_syscall.compat_utime.return = kernel.function("compat_sys_utime").return ?
{
	@_SYSCALL_COMPAT_UTIME_NAME
	retstr = return_str(1, $return)
}

# nd_compat_utime _____________________________________________________

probe nd_syscall.compat_utime = kprobe.function("compat_sys_utime") ?
{
	@_SYSCALL_COMPAT_UTIME_NAME
	asmlinkage()
	filename_uaddr = pointer_arg(1)
	filename = user_string_quoted(filename_uaddr)
	buf_uaddr = pointer_arg(2)
	actime = _struct_compat_utimbuf_actime(buf_uaddr)
	modtime = _struct_compat_utimbuf_modtime(buf_uaddr)
	@_SYSCALL_COMPAT_UTIME_ARGSTR
}
probe nd_syscall.compat_utime.return = kprobe.function("compat_sys_utime").return ?
{
	@_SYSCALL_COMPAT_UTIME_NAME
	retstr = returnstr(1)
}
