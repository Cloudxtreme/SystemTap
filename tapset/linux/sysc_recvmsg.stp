# recvmsg ____________________________________________________
#
# long sys_recvmsg(int fd,
#		 struct msghdr __user *msg,
#		 unsigned int flags)
#

@define _SYSCALL_RECVMSG_NAME
%(
	name = "recvmsg"
%)

@define _SYSCALL_RECVMSG_ARGSTR
%(
	argstr = sprintf("%d, %p, %s", s, msg_uaddr, flags_str)
%)

probe syscall.recvmsg = dw_syscall.recvmsg !, nd_syscall.recvmsg ? {}
probe syscall.recvmsg.return = dw_syscall.recvmsg.return !, nd_syscall.recvmsg.return ? {}

# dw_recvmsg _____________________________________________________

probe dw_syscall.recvmsg = __syscall.recvmsg ?, __syscall.socketcall.recvmsg ?
{
	# Avoid probe hits from compat_sys_socketcall() calling
	# compat_sys_recvmsg(), which sometimes calls
	# sys_recvmsg(). We could call __syscall_gate2() here with
	# NR_recvmsg and NR_socketcall, but all we really need to
	# check is that we're not in a compat task.
	@__syscall_gate_compat_simple
	@_SYSCALL_RECVMSG_NAME
	flags_str = _msg_flags_str(flags)
	@_SYSCALL_RECVMSG_ARGSTR
}
probe __syscall.recvmsg = kernel.function("sys_recvmsg").call ?
{
	@__syscall_gate(@const("__NR_recvmsg"))
	s = __int32($fd)
	msg_uaddr = $msg
	flags = __uint32($flags)
}
probe __syscall.socketcall.recvmsg = kernel.function("sys_socketcall").call ?
{
	if ($call != @const("SYS_RECVMSG")) next;
	s = __int32(user_ulong(&@cast($args, "ulong")[0]))
	msg_uaddr = user_ulong(&@cast($args, "ulong")[1])
	flags = __uint32(user_ulong(&@cast($args, "ulong")[2]))
}
probe dw_syscall.recvmsg.return = __syscall.recvmsg.return ?,
	__syscall.socketcall.recvmsg.return ?
{
	@__syscall_gate_compat_simple
	@_SYSCALL_RECVMSG_NAME
	retstr = return_str(1, $return)
}
probe __syscall.recvmsg.return = kernel.function("sys_recvmsg").return ?
{
	@__syscall_gate(@const("__NR_recvmsg"))
}
probe __syscall.socketcall.recvmsg.return =
	kernel.function("sys_socketcall").return ?
{
	if (@entry($call) != @const("SYS_RECVMSG")) next;
}

# nd_recvmsg _____________________________________________________

probe nd_syscall.recvmsg = __nd_syscall.recvmsg ?,
	__nd_syscall.socketcall.recvmsg ?
{
	# Avoid probe hits from compat_sys_socketcall() calling
	# compat_sys_recvmsg(), which sometimes calls
	# sys_recvmsg(). We could call __syscall_gate2() here with
	# NR_recvmsg and NR_socketcall, but all we really need to
	# check is that we're not in a compat task.
	@__syscall_gate_compat_simple

	@_SYSCALL_RECVMSG_NAME
	flags_str = _msg_flags_str(flags)
	@_SYSCALL_RECVMSG_ARGSTR
}
probe __nd_syscall.recvmsg = kprobe.function("sys_recvmsg").call ?
{
	@__syscall_gate(@const("__NR_recvmsg"))
	asmlinkage()
	s = int_arg(1)
	msg_uaddr = pointer_arg(2)
	flags = uint_arg(3)
}
probe __nd_syscall.socketcall.recvmsg = kprobe.function("sys_socketcall").call ?
{
	asmlinkage()
	if (int_arg(1) != @const("SYS_RECVMSG")) next;
	__args = &@cast(pointer_arg(2), "ulong", "kernel<linux/types.h>")
	s = __int32(user_ulong(&(__args)[0]))
	msg_uaddr = user_ulong(&(__args)[1])
	flags = __uint32(user_ulong(&(__args)[2]))
}
probe nd_syscall.recvmsg.return = __nd_syscall.recvmsg.return ?,
	__nd_syscall.socketcall.recvmsg.return ?
{
	@__syscall_gate_compat_simple
	@_SYSCALL_RECVMSG_NAME
	retstr = returnstr(1)
}
probe __nd_syscall.recvmsg.return = kprobe.function("sys_recvmsg").return ?
{
	@__syscall_gate(@const("__NR_recvmsg"))
}
probe __nd_syscall.socketcall.recvmsg.return =
	kprobe.function("sys_socketcall").return ?
{
	if (@entry(__asmlinkage_int_arg(1)) != @const("SYS_RECVMSG")) next;
}
